buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
    	classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

group = 'org.st1hy.android'
version = '1.0.3'

allprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'com.android.library'
    apply plugin: 'com.github.dcendents.android-maven'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 4
        versionName "1.0.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

Properties props = new Properties()
props.load(new FileInputStream(file(('.bintray'))))

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Gesture Detector'
            description 'Library for processing MotionEvents into various gestures'
            url 'https://github.com/st1hy/Gesture-Detector'

            packaging = ['jar']
            groupId 'org.st1hy.android'
            artifactId 'gesture-detector'
            version '1.0.3'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/st1hy/Gesture-Detector.git'
                url 'https://github.com/st1hy/Gesture-Detector'

            }
            developers {
                developer {
                    name 'st1hy'
                }
            }
        }
    }
}

bintray {
    user = props.getProperty('BINTRAY_USER')
    key = props.getProperty('BINTRAY_KEY')
    configurations = ['archives']
    publications = ['Maven']
    pkg {
        repo = 'maven'
        name = 'Gesture-Detector'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/st1hy/Gesture-Detector.git'
        version {
            name = '1.0.3'
            desc = 'Gesture Detector 1.0.3'
            vcsTag = '1.0.3'
        }
        dryRun = false
        publish = false
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main'
}

publishing {
    publications {
        Maven(MavenPublication) {
            groupId 'org.st1hy.android'
            artifactId 'gesture-detector'
            version '1.0.3'

            artifact(androidSourcesJar) {
                classifier "src"
                extension "zip"
            }
            artifact(androidJavadocsJar) {
                classifier "javadoc"
                extension "zip"
            }
        }
    }
}
