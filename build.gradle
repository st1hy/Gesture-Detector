buildscript {
    repositories {
        jcenter()
    }
    dependencies {
    	classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.3.1"
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 2
        versionName "1.0.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

Properties props = new Properties()
props.load(new FileInputStream(file(('.bintray'))))

group = 'org.st1hy.android'
version = '1.0.1'

bintray {
    user = props.getProperty('BINTRAY_USER')
    key = props.getProperty('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'Gesture-Detector'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/st1hy/Gesture-Detector.git'
        version {
            name = '1.0.1'
            desc = 'Gesture Detector 1.0.1'
            vcsTag = '1.0.1'
            released = new Date()
        }
        publications = ['MyPublication']
        configurations = ['archives']
        dryRun = false
        publish = false
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from 'src/main'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'org.st1hy.android'
            artifactId 'gesture-detector'
            version '1.0.1'

            artifact(androidSourcesJar) {
                classifier "src"
                extension "zip"
            }
            artifact(androidJavadocsJar) {
                classifier "javadoc"
                extension "zip"
            }
        }
    }
}
